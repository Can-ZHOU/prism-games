smg

// model parameters - prior probabilities
const double spoilt = 0.1; // milk spoilt
const double full = 0.4; // fridge full
const double out_of_stock0 = 0.05; // trader 0 out of stock
const double out_of_stock1 = 0.05; // trader 1 out of stock

system
	"Fridge" || "T0" || "T1"
endsystem

system "Fridge" fridge endsystem
system "T0" trader0 endsystem
system "T1" trader1 endsystem

//////////////// FRIDGE ////////////////////

const int traders = 2;
const int actions_per_trader = 2;
const int finished = traders*actions_per_trader*3 + 1;
const int shut_down = finished + 9;

module fridge
	
	s : [0..finished+3] init 0;

	// offers (P2)
	[o0?] s=0 -> (s'=0*3+1);
	[o1?] s=0 -> (s'=1*3+1);
	[o2?] s=0 -> (s'=2*3+1);
	[o3?] s=0 -> (s'=3*3+1);
	// also, listen for a termination signal, if no milk available (P2)
	[term?] s=0 -> (s'=shut_down); // synchronised between all traders

	// accept offers (P1)
	[a0!] s=0*3+1 -> 1-spoilt : (s'=0*3+2) + spoilt : (s'=0*3+3);
	[a1!] s=1*3+1 -> 1-spoilt : (s'=1*3+2) + spoilt : (s'=1*3+3);
	[a2!] s=2*3+1 -> 1-spoilt : (s'=2*3+2) + spoilt : (s'=2*3+3);
	[a3!] s=3*3+1 -> 1-spoilt : (s'=3*3+2) + spoilt : (s'=3*3+3);

	// signal whether milk is good or bad (dummy P2)
	[good0?] s=0*3+2 -> (s'=finished);
	[good1?] s=1*3+2 -> (s'=finished);
	[good2?] s=2*3+2 -> (s'=finished);
	[good3?] s=3*3+2 -> (s'=finished);

	[spoilt0?] s=0*3+3 -> (s'=0);
	[spoilt0?] s=1*3+3 -> (s'=0);
	[spoilt1?] s=2*3+3 -> (s'=0);
	[spoilt1?] s=3*3+3 -> (s'=0);

	// decline offers (P1)
	[d0!] s=0*3+1 -> (s'=finished);
	[d1!] s=1*3+1 -> (s'=finished);
	[d2!] s=2*3+1 -> (s'=finished);
	[d3!] s=3*3+1 -> (s'=finished);

	[transaction_finished0?] s=finished -> 1-full : (s'=finished+1) + full : (s'=finished+2);
	[transaction_finished1?] s=finished -> 1-full : (s'=finished+1) + full : (s'=finished+2);

	// continue (dummy P2)
	[continue0?] s=finished+1 -> (s'=0);
	[continue1?] s=finished+1 -> (s'=0);


	// SHUT DOWN GADGET - allow each non-finished trader to go through one cycle of offerings; each of which is declined
	// two traders, so need one more stage
	[term?] s=finished+2 -> (s'=shut_down);
	[o0?] s=finished+2 -> (s'=0*1+finished+3);
	[d0!] s=0*1+finished+3 -> (s'=finished+7);
	[o1?] s=finished+2 -> (s'=1*1+finished+3);
	[d1!] s=1*1+finished+3 -> (s'=finished+7);
	[o2?] s=finished+2 -> (s'=2*1+finished+3);
	[d2!] s=2*1+finished+3 -> (s'=finished+7);
	[o3?] s=finished+2 -> (s'=3*1+finished+3);
	[d3!] s=3*1+finished+3 -> (s'=finished+7);

	[transaction_finished0?] s=finished+7 -> (s'=finished+8);
	[transaction_finished1?] s=finished+7 -> (s'=finished+8);

	[term?] s=finished+8 -> (s'=shut_down);

	// terminal (P2)
	[term?] s>=shut_down-> true;

endmodule

//////////////// TRADER 0 ////////////////////

module trader0
       
	t0 : [-1..5] init -1;
       
	// check supply (P1)
	[check_supply0!] t0=-1 -> 1-out_of_stock0 : (t0'=0) + out_of_stock0 : (t0'=5);

	// offers (P1)
	[o0!] t0=0 -> (t0'=1);
	[o1!] t0=0 -> (t0'=2);

	// accept offers (P2)
	[a0?] t0=1 -> (t0'=3);
	[a1?] t0=2 -> (t0'=3);

	// decline offers (P2)
	[d0?] t0=1 -> (t0'=3);
	[d1?] t0=2 -> (t0'=3);

	// P2 decides on stopping
	[spoilt0?] t0=3 -> (t0'=-1);
	[transaction_finished0?] t0=3 -> (t0'=4);

	[term?] t0=4 -> (t0'=5);
	[continue0?] t0=4 -> (t0'=-1);

	// terminal (P2)
	[term?] t0>=5-> true;

endmodule


//////////////// TRADER 1 ////////////////////

module trader1
       
	t1 : [-1..5] init -1;
       
	// check supply (P1)
	[check_supply1!] t1=-1 -> 1-out_of_stock1 : (t1'=0) + out_of_stock1 : (t1'=5);

	// offers (P1)
	[o2!] t1=0 -> (t1'=1);
	[o3!] t1=0 -> (t1'=2);

	// accept offers (P2)
	[a2?] t1=1 -> (t1'=3);
	[a3?] t1=2 -> (t1'=3);

	// decline offers (P2)
	[d2?] t1=1 -> (t1'=3);
	[d3?] t1=2 -> (t1'=3);

	// P2 decides on stopping
	[spoilt1?] t1=3 -> (t1'=-1);
	[transaction_finished1?] t1=3 -> (t1'=4);

	[term?] t1=4 -> (t1'=5);
	[continue1?] t1=4 -> (t1'=-1);

	// terminal (P2)
	[term?] t1>=5-> true;

endmodule


// count how often an offer is accepted by the fridge (A_i)
rewards "accept0"
	[a0] true : 1;
	[a1] true : 1;
endrewards

rewards "accept1"
	[a2] true : 1;
	[a3] true : 1;
endrewards


// quantity of milk obtained by the fridge (N)
rewards "quantity"
	[good0] true : 1.5;
	[good1] true : 6;
	[good2] true : 1.5;
	[good3] true : 6;
endrewards


// the money obtained by the traders (per unit of milk) (M_i)
rewards "money0"
	[a0] true : 1;
	[a1] true : 0.5;
endrewards

rewards "money1"
	[a2] true : 1;
	[a3] true : 0.5;
endrewards


// the quality of the offers made by the traders (Q_i)
rewards "quality0"
	[o0] true : 0.5;
	[o1] true : 2;
endrewards

rewards "quality1"
	[o2] true : 0.5;
	[o3] true : 2;
endrewards


// counting the offers made by the traders (O_i)
rewards "offers0"
	[o0] true : 1;
	[o1] true : 1;
endrewards

rewards "offers1"
	[o2] true : 1;
	[o3] true : 1;
endrewards
